// © 2022 NVIDIA Corporation

#include "Shared.hlsli"
#include "RaytracingShared.hlsli"

// Inputs
NRI_RESOURCE( Texture2D<float3>, gIn_PrevComposedDiff, t, 0, SET_OTHER );
NRI_RESOURCE( Texture2D<float4>, gIn_PrevComposedSpec_PrevViewZ, t, 1, SET_OTHER );

// Outputs
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_Mv, u, 0, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float>, gOut_ViewZ, u, 1, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_Normal_Roughness, u, 2, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_BaseColor_Metalness, u, 3, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float3>, gOut_DirectLighting, u, 4, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float3>, gOut_DirectEmission, u, 5, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float3>, gOut_PsrThroughput, u, 6, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float2>, gOut_ShadowData, u, 7, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_Shadow_Translucency, u, 8, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_Diff, u, 9, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_Spec, u, 10, SET_OTHER );

#if( NRD_MODE == SH )
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_DiffSh, u, 11, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_SpecSh, u, 12, SET_OTHER );
#endif

float4 GetRadianceFromPreviousFrame( GeometryProps geometryProps, MaterialProps materialProps, uint2 pixelPos )
{
    float3 prevLdiff, prevLspec;
    float prevFrameWeight = ReprojectIrradiance( true, false, gIn_PrevComposedDiff, gIn_PrevComposedSpec_PrevViewZ, geometryProps, pixelPos, prevLdiff, prevLspec );

    // Estimate how strong lighting at hit depends on the view direction
    float normCurvature = saturate( sqrt( abs( materialProps.curvature ) ) / 2.5 );
    float specConfidence = GetSpecMagicCurve( materialProps.roughness );
    specConfidence = lerp( specConfidence, 1.0, normCurvature );

    float diffLum = Color::Luminance( prevLdiff );
    float specLum = Color::Luminance( prevLspec );
    float specWeight = specLum / ( diffLum + specLum + NRD_EPS );

    prevFrameWeight *= lerp( 1.0, specConfidence, specWeight );

    float3 prevLsum = prevLdiff + prevLspec * specConfidence;

    // Avoid really bad reprojection
    prevLsum *= saturate( prevFrameWeight / 0.05 );

    return float4( prevLsum, prevFrameWeight );
}

float GetMaterialID( GeometryProps geometryProps, MaterialProps materialProps )
{
    bool isHair = geometryProps.Has( FLAG_HAIR );
    bool isMetal = materialProps.metalness > 0.5;

    return isHair ? MATERIAL_ID_HAIR : ( isMetal ? MATERIAL_ID_METAL : MATERIAL_ID_DEFAULT );
}

//========================================================================================
// TRACE OPAQUE
//========================================================================================

/*
The function has not been designed to trace primary hits. But still can be used to trace
direct and indirect lighting.

Prerequisites:
    Rng::Hash::Initialize( )

Derivation:
    Lsum = L0 + BRDF0 * ( L1 + BRDF1 * ( L2 + BRDF2 * ( L3 +  ... ) ) )

    Lsum = L0 +
        L1 * BRDF0 +
        L2 * BRDF0 * BRDF1 +
        L3 * BRDF0 * BRDF1 * BRDF2 +
        ...
*/

struct TraceOpaqueResult
{
    float3 diffRadiance;
    float diffHitDist;

    float3 specRadiance;
    float specHitDist;

#if( NRD_MODE == SH || NRD_MODE == DIRECTIONAL_OCCLUSION )
    float3 diffDirection;
    float3 specDirection;
#endif
};

TraceOpaqueResult TraceOpaque( GeometryProps geometryProps0, MaterialProps materialProps0, uint2 pixelPos, float3x3 mirrorMatrix, float4 Lpsr )
{
    TraceOpaqueResult result = ( TraceOpaqueResult )0;
#if( NRD_MODE < OCCLUSION )
    result.specHitDist = NRD_FrontEnd_SpecHitDistAveraging_Begin( );
#endif

    GeometryProps geometryProps = geometryProps0;
    MaterialProps materialProps = materialProps0;
    float viewZ0 = Geometry::AffineTransform( gWorldToView, geometryProps.X ).z;
    float roughness0 = materialProps.roughness;

    // Material de-modulation ( convert irradiance into radiance )
    float3 diffFactor0, specFactor0;
    {
        float3 albedo, Rf0;
        BRDF::ConvertBaseColorMetalnessToAlbedoRf0( materialProps.baseColor, materialProps.metalness, albedo, Rf0 );

        GetMaterialFactors( materialProps.N, geometryProps.V, albedo, Rf0, roughness0, geometryProps.Has( FLAG_HAIR ), diffFactor0, specFactor0 );
    }

    // SHARC debug visualization
#if( USE_SHARC_DEBUG != 0 )
    HashGridParameters hashGridParameters;
    hashGridParameters.cameraPosition = gCameraGlobalPos.xyz;
    hashGridParameters.sceneScale = SHARC_SCENE_SCALE;
    hashGridParameters.logarithmBase = SHARC_GRID_LOGARITHM_BASE;
    hashGridParameters.levelBias = SHARC_GRID_LEVEL_BIAS;

    SharcHitData sharcHitData;
    sharcHitData.positionWorld = GetGlobalPos( geometryProps.X );
    sharcHitData.materialDemodulation = GetMaterialFactor( geometryProps, materialProps );
    sharcHitData.normalWorld = geometryProps.N;
    sharcHitData.emissive = materialProps.Lemi;

    HashGridData hashGridData;
    hashGridData.capacity = SHARC_CAPACITY;
    hashGridData.hashEntriesBuffer = gInOut_SharcHashEntriesBuffer;

    SharcParameters sharcParams;
    sharcParams.hashGridParameters = hashGridParameters;
    sharcParams.hashGridData = hashGridData;
    sharcParams.radianceScale = SHARC_RADIANCE_SCALE;
    sharcParams.accumulationBuffer = gInOut_SharcAccumulated;
    sharcParams.resolvedBuffer = gInOut_SharcResolved;

    float3 color;
    #if( USE_SHARC_DEBUG == 2 )
        color = HashGridDebugColoredHash( sharcHitData.positionWorld, sharcHitData.normalWorld, hashGridParameters );
    #else
        bool isValid = SharcGetCachedRadiance( sharcParams, sharcHitData, color, true );

        // Highlight invalid cells
        // color = isValid ?  color : float3( 1.0, 0.0, 0.0 );
    #endif

    result.diffRadiance = color / diffFactor0;

    return result;
#endif

    uint checkerboard = Sequence::CheckerBoard( pixelPos, gFrameIndex ) != 0;
    uint pathNum = gSampleNum << (gTracingMode == RESOLUTION_FULL ? 1 : 0);
    uint diffPathNum = 0;

[loop]
for( uint path = 0; path < pathNum; path++ )
{
    float accumulatedHitDist = 0;
    float accumulatedDiffuseLikeMotion = 0;
    float accumulatedCurvature = 0;

    float3 Lsum = Lpsr.xyz;
    float3 pathThroughput = 1.0 - Lpsr.w;
    bool isDiffusePath = false;

    [loop]
    for( uint bounce = 1; bounce <= gBounceNum && !geometryProps.IsMiss( ); bounce++ )
    {
        //=============================================================================================================================================================
        // Origin point
        //=============================================================================================================================================================

        bool isDiffuse = false;
        float lobeTanHalfAngleAtOrigin = 0.0;
        {
            // Diffuse probability
            float diffuseProbability = EstimateDiffuseProbability( geometryProps, materialProps );

            // Clamp probability to a sane range ( for all bounces ) to reduce noise and convergence time
            diffuseProbability = float( diffuseProbability != 0.0 ) * clamp( diffuseProbability, gMinProbability, 1.0 - gMinProbability );

            // Diffuse or specular?
            float rnd = Rng::Hash::GetFloat( );
            if( bounce == 1 && !gRR && gTracingMode == RESOLUTION_FULL_PROBABILISTIC )
            {
                // Guarantee a sample in 3x3 area ( for the 1st bounce, see NRD docs )
                float bayer = Sequence::Bayer4x4( pixelPos, gFrameIndex );
                float jitter = Sequence::Weyl1D( rsqrt( 7.0 ), gFrameIndex ); // screen-uniform

                // Fix harmonic interference of "bayer" and "blue noise" ( i.e. decorrelate ), which are both "pow-of-2" structures ( it doesn't break white noise )
                rnd = frac( bayer + jitter );
            }

            isDiffuse = rnd < diffuseProbability;

            if( gTracingMode == RESOLUTION_FULL_PROBABILISTIC || bounce > 1 )
                pathThroughput /= isDiffuse ? diffuseProbability : ( 1.0 - diffuseProbability );
            else
                isDiffuse = gTracingMode == RESOLUTION_HALF ? checkerboard : ( path & 0x1 );

            // This is not needed in case of "RESOLUTION_FULL_PROBABILISTIC", since hair doesn't have diffuse component
            if( geometryProps.Has( FLAG_HAIR ) && isDiffuse )
                break;

            // Importance sampling
            uint sampleMaxNum = 0;
            if( gDisableShadowsAndEnableImportanceSampling && ( USE_IS_FOR_ALL_BOUNCES || bounce == 1 ) )
                sampleMaxNum = PT_IMPORTANCE_SAMPLES_NUM * ( isDiffuse ? 1.0 : GetSpecMagicCurve( materialProps.roughness ) );
            sampleMaxNum = max( sampleMaxNum, 1 );

            float3 ray = GenerateRayAndUpdateThroughput( geometryProps, materialProps, pathThroughput, sampleMaxNum, isDiffuse, pixelPos, path, bounce, GR_HAIR | GR_ALLOW_BN  );

            // Special case for primary surface ( 1st bounce starts here )
            if( bounce == 1 )
            {
                isDiffusePath = isDiffuse;

                if( gTracingMode == RESOLUTION_FULL )
                    Lsum *= isDiffuse ? diffuseProbability : ( 1.0 - diffuseProbability );

                // ( Optional ) Save sampling direction for the 1st bounce
                #if( NRD_MODE == SH || NRD_MODE == DIRECTIONAL_OCCLUSION )
                    float3 psrRay = Geometry::RotateVectorInverse( mirrorMatrix, ray );

                    if( isDiffuse )
                        result.diffDirection += psrRay;
                    else
                        result.specDirection += psrRay;
                #endif
            }

            // Abort tracing if the current bounce contribution is low
        #if( USE_RUSSIAN_ROULETTE == 1 )
            /*
            BAD PRACTICE:
            Russian Roulette approach is here to demonstrate that it's a bad practice for real time denoising for the following reasons:
            - increases entropy of the signal
            - transforms radiance into non-radiance, which is strictly speaking not allowed to be processed spatially (who wants to get a high energy firefly
            redistributed around surrounding pixels?)
            - not necessarily converges to the right image, because we do assumptions about the future and approximate the tail of the path via a scaling factor
            - this approach breaks denoising, especially REBLUR, which has been designed to work with pure radiance
            */

            // Nevertheless, RR can be used with caution: the code below tuned for good IQ / PERF tradeoff
            float russianRouletteProbability = Color::Luminance( pathThroughput );
            russianRouletteProbability = Math::Pow01( russianRouletteProbability, 0.25 );
            russianRouletteProbability = max( russianRouletteProbability, 0.01 );

            if( Rng::Hash::GetFloat( ) > russianRouletteProbability )
                break;

            pathThroughput /= russianRouletteProbability;
        #else
            /*
            GOOD PRACTICE:
            - terminate path if "pathThroughput" is smaller than some threshold
            - approximate ambient at the end of the path
            - re-use data from the previous frame
            */

            if( PT_THROUGHPUT_THRESHOLD != 0.0 && Color::Luminance( pathThroughput ) < PT_THROUGHPUT_THRESHOLD )
                break;
        #endif

            //=========================================================================================================================================================
            // Trace to the next hit
            //=========================================================================================================================================================

            float roughnessTemp = isDiffuse ? 1.0 : materialProps.roughness;
            lobeTanHalfAngleAtOrigin = roughnessTemp * roughnessTemp / ( 1.0 + roughnessTemp * roughnessTemp );

            float2 mipAndCone = GetConeAngleFromRoughness( geometryProps.mip, isDiffuse ? 1.0 : materialProps.roughness );
            geometryProps = CastRay( GetXoffset( geometryProps.X, geometryProps.N ), ray, 0.0, INF, mipAndCone, gWorldTlas, FLAG_NON_TRANSPARENT, PT_RAY_FLAGS );
            materialProps = GetMaterialProps( geometryProps ); // TODO: try to read metrials only if L1- and L2- lighting caches failed
        }

        //=============================================================================================================================================================
        // Hit point
        //=============================================================================================================================================================

        {
            //=============================================================================================================================================================
            // Lighting
            //=============================================================================================================================================================

            float4 Lcached = float4( materialProps.Lemi, 0.0 );
            if( !geometryProps.IsMiss( ) )
            {
                // L1 cache - reproject previous frame, carefully treating specular
                Lcached = GetRadianceFromPreviousFrame( geometryProps, materialProps, pixelPos );

                // L2 cache - SHARC
                HashGridParameters hashGridParameters;
                hashGridParameters.cameraPosition = gCameraGlobalPos.xyz;
                hashGridParameters.sceneScale = SHARC_SCENE_SCALE;
                hashGridParameters.logarithmBase = SHARC_GRID_LOGARITHM_BASE;
                hashGridParameters.levelBias = SHARC_GRID_LEVEL_BIAS;

                float3 Xglobal = GetGlobalPos( geometryProps.X );
                uint level = HashGridGetLevel( Xglobal, hashGridParameters );
                float voxelSize = HashGridGetVoxelSize( level, hashGridParameters );

                float footprint = geometryProps.hitT * lobeTanHalfAngleAtOrigin * 2.0;
                float footprintNorm = saturate( footprint / voxelSize );

                float2 rndScaled = ImportanceSampling::Cosine::GetRay( Rng::Hash::GetFloat2( ) ).xy;
                rndScaled *= 1.0 - footprintNorm; // reduce dithering if cone is already wide
                rndScaled *= voxelSize;
                rndScaled *= 1.5;
                rndScaled *= USE_SHARC_DITHERING;

                float3x3 mBasis = Geometry::GetBasis( geometryProps.N );
                float3 jitter = mBasis[ 0 ] * rndScaled.x + mBasis[ 1 ] * rndScaled.y;

                SharcHitData sharcHitData;
                sharcHitData.materialDemodulation = GetMaterialFactor( geometryProps, materialProps );
                sharcHitData.normalWorld = geometryProps.N;
                sharcHitData.emissive = materialProps.Lemi;

                HashGridData hashGridData;
                hashGridData.capacity = SHARC_CAPACITY;
                hashGridData.hashEntriesBuffer = gInOut_SharcHashEntriesBuffer;

                SharcParameters sharcParams;
                sharcParams.hashGridParameters = hashGridParameters;
                sharcParams.hashGridData = hashGridData;
                sharcParams.radianceScale = SHARC_RADIANCE_SCALE;
                sharcParams.accumulationBuffer = gInOut_SharcAccumulated;
                sharcParams.resolvedBuffer = gInOut_SharcResolved;

                bool isSharcAllowed = !geometryProps.Has( FLAG_HAIR ); // ignore if the hit is hair // TODO: if hair don't allow if hitT is too short
                isSharcAllowed &= Rng::Hash::GetFloat( ) > Lcached.w; // is needed?
                isSharcAllowed &= Rng::Hash::GetFloat( ) < ( bounce == gBounceNum ? 1.0 : footprintNorm ); // is voxel size acceptable?
                isSharcAllowed &= gSHARC && NRD_MODE < OCCLUSION; // trivial

                if( isSharcAllowed )
                {
                    float3 sharcRadiance = 0;

                    // Try jittered position
                    sharcHitData.positionWorld = Xglobal + jitter;
                    bool isFound = SharcGetCachedRadiance( sharcParams, sharcHitData, sharcRadiance, false );

                    if( !isFound )
                    {
                        // Slipped out of the surface or mismatched normals, try non-jittered position
                        sharcHitData.positionWorld = Xglobal;
                        isFound = SharcGetCachedRadiance( sharcParams, sharcHitData, sharcRadiance, false );
                    }

                    // Apply occlusion estimation for the last bounce
                #if USE_AO_FOR_LAST_BOUNCE
                    if( bounce == gBounceNum )
                        sharcRadiance *= Math::Sqrt01( geometryProps.hitT / voxelSize );
                #endif

                    if( isFound )
                        Lcached = float4( sharcRadiance, 1.0 );
                }

                // Cache miss - compute lighting, if not found in caches
                if( Rng::Hash::GetFloat( ) > Lcached.w )
                {
                    float3 L = GetLighting( geometryProps, materialProps, LIGHTING | SHADOW ) + materialProps.Lemi;
                    Lcached.xyz = bounce < gBounceNum ? L : max( Lcached.xyz, L );
                }
            }

            //=============================================================================================================================================================
            // Other
            //=============================================================================================================================================================

            // Accumulate lighting
            float3 L = Lcached.xyz * pathThroughput;
            Lsum += L;

            // ( Biased ) Reduce contribution of next samples if previous frame is sampled, which already has multi-bounce information
            pathThroughput *= 1.0 - Lcached.w;

            // Accumulate path length for NRD ( see "README/NOISY INPUTS" )
            float a = Color::Luminance( L );
            float b = Color::Luminance( Lsum ); // already includes L
            float importance = a / ( b + 1e-6 );

            importance *= 1.0 - Color::Luminance( materialProps.Lemi ) / ( a + 1e-6 );

            float diffuseLikeMotion = EstimateDiffuseProbability( geometryProps, materialProps, true );
            diffuseLikeMotion = isDiffuse ? 1.0 : diffuseLikeMotion;

            accumulatedHitDist += ApplyThinLensEquation( geometryProps.hitT, accumulatedCurvature ) * Math::SmoothStep( 0.2, 0.0, accumulatedDiffuseLikeMotion );
            accumulatedDiffuseLikeMotion += 1.0 - importance * ( 1.0 - diffuseLikeMotion );
            accumulatedCurvature += materialProps.curvature; // yes, after hit

        #if( USE_CAMERA_ATTACHED_REFLECTION_TEST == 1 && NRD_NORMAL_ENCODING == NRD_NORMAL_ENCODING_R10G10B10A2_UNORM )
            // IMPORTANT: lazy ( no checkerboard support ) implementation of reflections masking for objects attached to the camera
            // TODO: better find a generic solution for tracking of reflections for objects attached to the camera
            if( bounce == 1 && !isDiffuse && desc.materialProps.roughness < 0.01 )
            {
                if( !geometryProps.IsMiss( ) && !geometryProps.Has( FLAG_STATIC ) )
                    gOut_Normal_Roughness[ desc.pixelPos ].w = MATERIAL_ID_SELF_REFLECTION;
            }
        #endif
        }
    }

    // Debug visualization: specular mip level at the end of the path
    if( gOnScreen == SHOW_MIP_SPECULAR )
    {
        float mipNorm = Math::Sqrt01( geometryProps.mip / MAX_MIP_LEVEL );
        Lsum = Color::ColorizeZucconi( mipNorm );
    }

    // Normalize hit distances for REBLUR before averaging
    float normHitDist = accumulatedHitDist;
    if( gDenoiserType != DENOISER_RELAX )
        normHitDist = REBLUR_FrontEnd_GetNormHitDist( accumulatedHitDist, viewZ0, gHitDistSettings.xyz, isDiffusePath ? 1.0 : roughness0 );

    // Accumulate diffuse and specular separately for denoising
    if( !USE_SANITIZATION || NRD_IsValidRadiance( Lsum ) )
    {
        if( isDiffusePath )
        {
            result.diffRadiance += Lsum;
            result.diffHitDist += normHitDist;
            diffPathNum++;
        }
        else
        {
            result.specRadiance += Lsum;

        #if( NRD_MODE < OCCLUSION )
            NRD_FrontEnd_SpecHitDistAveraging_Add( result.specHitDist, normHitDist );
        #else
            result.specHitDist += normHitDist;
        #endif
        }
    }

    // Return back to the start of the path
    geometryProps = geometryProps0;
    materialProps = materialProps0;
}

    // Material de-modulation ( convert irradiance into radiance )
    result.diffRadiance /= diffFactor0;
    result.specRadiance /= specFactor0;

    // Radiance is already divided by sampling probability, we need to average across all paths
    float radianceNorm = 1.0 / float( gSampleNum );
    result.diffRadiance *= radianceNorm;
    result.specRadiance *= radianceNorm;

    // Others are not divided by sampling probability, we need to average across diffuse / specular only paths
    float diffNorm = diffPathNum == 0 ? 0.0 : 1.0 / float( diffPathNum );
    float specNorm = pathNum == diffPathNum ? 0.0 : 1.0 / float( pathNum - diffPathNum );

    result.diffHitDist *= diffNorm;

#if( NRD_MODE < OCCLUSION )
    NRD_FrontEnd_SpecHitDistAveraging_End( result.specHitDist );
#else
    result.specHitDist *= specNorm;
#endif

#if( NRD_MODE == SH || NRD_MODE == DIRECTIONAL_OCCLUSION )
    result.diffDirection *= diffNorm;
    result.specDirection *= specNorm;
#endif

    return result;
}

//========================================================================================
// MAIN
//========================================================================================

void WriteResult( uint2 pixelPos, float4 diff, float4 spec, float4 diffSh, float4 specSh )
{
    uint2 outPixelPos = pixelPos;
    if( gTracingMode == RESOLUTION_HALF )
        outPixelPos.x >>= 1;

    uint checkerboard = Sequence::CheckerBoard( pixelPos, gFrameIndex ) != 0;

    if( gTracingMode == RESOLUTION_HALF )
    {
        if( checkerboard )
        {
            gOut_Diff[ outPixelPos ] = diff;

        #if( NRD_MODE == SH )
            gOut_DiffSh[ outPixelPos ] = diffSh;
        #endif
        }
        else
        {
            gOut_Spec[ outPixelPos ] = spec;

        #if( NRD_MODE == SH )
            gOut_SpecSh[ outPixelPos ] = specSh;
        #endif
        }
    }
    else
    {
        gOut_Diff[ outPixelPos ] = diff;
        gOut_Spec[ outPixelPos ] = spec;

    #if( NRD_MODE == SH )
        gOut_DiffSh[ outPixelPos ] = diffSh;
        gOut_SpecSh[ outPixelPos ] = specSh;
    #endif
    }
}

[numthreads( 16, 16, 1 )]
void main( uint2 pixelPos : SV_DispatchThreadID )
{
    // Pixel and sample UV
    float2 pixelUv = float2( pixelPos + 0.5 ) * gInvRectSize;
    float2 sampleUv = pixelUv + gJitter;

    // Do not generate NANs for unused threads
    if( pixelUv.x > 1.0 || pixelUv.y > 1.0 )
    {
    #if( USE_DRS_STRESS_TEST == 1 )
        WriteResult( pixelPos, GARBAGE, GARBAGE, GARBAGE, GARBAGE );
    #endif

        return;
    }

    // Initialize RNG
    Rng::Hash::Initialize( pixelPos, gFrameIndex );

    //================================================================================================================================================================================
    // Primary ray
    //================================================================================================================================================================================

    float3 cameraRayOrigin = 0;
    float3 cameraRayDirection = 0;
    GetCameraRay( cameraRayOrigin, cameraRayDirection, sampleUv );

    GeometryProps geometryProps0 = CastRay( cameraRayOrigin, cameraRayDirection, 0.0, INF, GetConeAngleFromRoughness( 0.0, 0.0 ), gWorldTlas, ( gOnScreen == SHOW_INSTANCE_INDEX || gOnScreen == SHOW_NORMAL ) ? GEOMETRY_ALL : FLAG_NON_TRANSPARENT, 0 );
    MaterialProps materialProps0 = GetMaterialProps( geometryProps0 );

    //================================================================================================================================================================================
    // Primary surface replacement ( aka jump through mirrors )
    //================================================================================================================================================================================

    float3 psrThroughput = 1.0;
    float3x3 mirrorMatrix = Geometry::GetMirrorMatrix( 0 ); // identity
    float accumulatedHitDist = 0.0;
    float accumulatedCurvature = 0.0;
    uint bounceNum = PT_PSR_BOUNCES_NUM;

    float3 X0 = geometryProps0.X;
    float3 V0 = geometryProps0.V;
    float viewZ0 = Geometry::AffineTransform( gWorldToView, geometryProps0.X ).z;

    bool isTaa5x5 = geometryProps0.Has( FLAG_HAIR | FLAG_SKIN ) || geometryProps0.IsMiss( ); // switched TAA to "higher quality & slower response" mode
    float viewZAndTaaMask0 = abs( viewZ0 ) * FP16_VIEWZ_SCALE * ( isTaa5x5 ? -1.0 : 1.0 );

    [loop]
    while( bounceNum && !geometryProps0.IsMiss( ) && IsDelta( materialProps0 ) && gPSR )
    {
        { // Origin point
            // Accumulate curvature
            accumulatedCurvature += materialProps0.curvature; // yes, before hit

            // Accumulate mirror matrix
            mirrorMatrix = mul( Geometry::GetMirrorMatrix( materialProps0.N ), mirrorMatrix );

            // Choose a ray
            float3 ray = reflect( -geometryProps0.V, materialProps0.N );

            // Update throughput
            float3 albedo, Rf0;
            BRDF::ConvertBaseColorMetalnessToAlbedoRf0( materialProps0.baseColor, materialProps0.metalness, albedo, Rf0 );

            float NoV = abs( dot( materialProps0.N, geometryProps0.V ) );
            float3 Fenv = BRDF::EnvironmentTerm_Rtg( Rf0, NoV, materialProps0.roughness );

            psrThroughput *= Fenv;

            // Trace to the next hit
            float2 mipAndCone = GetConeAngleFromRoughness( geometryProps0.mip, materialProps0.roughness );
            geometryProps0 = CastRay( GetXoffset( geometryProps0.X, geometryProps0.N ), ray, 0.0, INF, mipAndCone, gWorldTlas, FLAG_NON_TRANSPARENT, PT_RAY_FLAGS );
            materialProps0 = GetMaterialProps( geometryProps0 );
        }

        { // Hit point
            // Accumulate hit distance representing virtual point position ( see "README/NOISY INPUTS" )
            accumulatedHitDist += ApplyThinLensEquation( geometryProps0.hitT, accumulatedCurvature ) ; // TODO: take updated from NRD
        }

        bounceNum--;
    }

    //================================================================================================================================================================================
    // G-buffer ( guides )
    //================================================================================================================================================================================

    // Motion
    float3 Xvirtual = X0 - V0 * accumulatedHitDist;
    float3 XvirtualPrev = Xvirtual + geometryProps0.Xprev - geometryProps0.X;
    float3 motion = GetMotion( Xvirtual, XvirtualPrev );

    gOut_Mv[ pixelPos ] = float4( motion, viewZAndTaaMask0 ); // IMPORTANT: keep viewZ before PSR ( needed for glass )

    // ViewZ
    float viewZ = Geometry::AffineTransform( gWorldToView, Xvirtual ).z;
    viewZ = geometryProps0.IsMiss( ) ? Math::Sign( viewZ ) * INF : viewZ;

    gOut_ViewZ[ pixelPos ] = viewZ;

    // Emission
    gOut_DirectEmission[ pixelPos ] = materialProps0.Lemi * psrThroughput;

    // Early out
    if( geometryProps0.IsMiss( ) )
    {
    #if( USE_INF_STRESS_TEST == 1 )
        WriteResult( pixelPos, GARBAGE, GARBAGE, GARBAGE, GARBAGE );
    #endif

        return;
    }

    // Normal, roughness and material ID
    float3 N = Geometry::RotateVectorInverse( mirrorMatrix, materialProps0.N );
#if( RTXCR_INTEGRATION == 1 )
    if( geometryProps0.Has( FLAG_HAIR ) )
    {
        // Generate a better guide for hair
        float3 B = cross( geometryProps0.V, geometryProps0.T.xyz );
        float3 n = normalize( cross( geometryProps0.T.xyz, B ) );

        float pixelSize = gUnproject * lerp( abs( viewZ ), 1.0, abs( gOrthoMode ) );
        float f = NRD_GetNormalizedStrandThickness( STRAND_THICKNESS / gUnitToMetersMultiplier, pixelSize );
        f = lerp( 0.0, 0.25, f );

        N = normalize( lerp( n, N, f ) );
    }
#endif

    float materialID = GetMaterialID( geometryProps0, materialProps0 );
#if( USE_SIMULATED_MATERIAL_ID_TEST == 1 )
    materialID = frac( geometryProps0.X ).x < 0.05 ? MATERIAL_ID_HAIR : materialID;
#endif

    gOut_Normal_Roughness[ pixelPos ] = NRD_FrontEnd_PackNormalAndRoughness( N, materialProps0.roughness, materialID );

    // Base color and metalness
    gOut_BaseColor_Metalness[ pixelPos ] = float4( Color::ToSrgb( materialProps0.baseColor ), materialProps0.metalness );

    // Direct lighting
    float3 Xshadow;
    float3 Ldirect = GetLighting( geometryProps0, materialProps0, LIGHTING | SSS, Xshadow );

    if( gOnScreen == SHOW_INSTANCE_INDEX )
    {
        Rng::Hash::Initialize( geometryProps0.instanceIndex, 0 );

        uint checkerboard = Sequence::CheckerBoard( pixelPos >> 2, 0 ) != 0;
        float3 color = Rng::Hash::GetFloat4( ).xyz;
        color *= ( checkerboard && !geometryProps0.Has( FLAG_STATIC ) ) ? 0.5 : 1.0;

        Ldirect = color;
    }
    else if( gOnScreen == SHOW_UV )
        Ldirect = float3( frac( geometryProps0.uv ), 0 );
    else if( gOnScreen == SHOW_CURVATURE )
        Ldirect = sqrt( abs( materialProps0.curvature ) ) * 0.1;
    else if( gOnScreen == SHOW_MIP_PRIMARY )
    {
        float mipNorm = Math::Sqrt01( geometryProps0.mip / MAX_MIP_LEVEL );
        Ldirect = Color::ColorizeZucconi( mipNorm );
    }

    gOut_DirectLighting[ pixelPos ] = Ldirect; // "psrThroughput" applied in "Composition"
    gOut_PsrThroughput[ pixelPos ] = psrThroughput;

    // Lighting at PSR hit, if found
    float4 Lpsr = 0;
    if( !geometryProps0.IsMiss( ) && bounceNum != PT_PSR_BOUNCES_NUM )
    {
        // L1 cache - reproject previous frame, carefully treating specular
        Lpsr = GetRadianceFromPreviousFrame( geometryProps0, materialProps0, pixelPos );

        // Subtract direct lighting, process it separately
        float3 L = Ldirect * GetLighting( geometryProps0, materialProps0, SHADOW ) + materialProps0.Lemi;
        Lpsr.xyz = max( Lpsr.xyz - L, 0.0 );

        // TODO: it's not a 100% fix
        if( gTracingMode == RESOLUTION_HALF )
            Lpsr *= 0.5;

        // This is important!
        Lpsr.xyz *= Lpsr.w;
    }

    //================================================================================================================================================================================
    // Secondary rays
    //================================================================================================================================================================================

    TraceOpaqueResult result = TraceOpaque( geometryProps0, materialProps0, pixelPos, mirrorMatrix, Lpsr );

#if( USE_MOVING_EMISSION_FIX == 1 )
    // Or emissives ( not having lighting in diffuse and specular ) can use a different material ID
    result.diffRadiance += materialProps0.Lemi / Math::Pi( 2.0 );
    result.specRadiance += materialProps0.Lemi / Math::Pi( 2.0 );
#endif

#if( USE_SIMULATED_MATERIAL_ID_TEST == 1 )
    if( frac( geometryProps0.X ).x < 0.05 )
        result.diffRadiance = float3( 0, 10, 0 ) * Color::Luminance( result.diffRadiance );
#endif

#if( USE_SIMULATED_FIREFLY_TEST == 1 )
    const float maxFireflyEnergyScaleFactor = 10000.0;
    result.diffRadiance /= lerp( 1.0 / maxFireflyEnergyScaleFactor, 1.0, Rng::Hash::GetFloat( ) );
#endif

    float4 outDiff = 0.0;
    float4 outSpec = 0.0;
    float4 outDiffSh = 0.0;
    float4 outSpecSh = 0.0;

    if( gDenoiserType == DENOISER_RELAX )
    {
    #if( NRD_MODE == SH )
        outDiff = RELAX_FrontEnd_PackSh( result.diffRadiance, result.diffHitDist, result.diffDirection, outDiffSh, USE_SANITIZATION );
        outSpec = RELAX_FrontEnd_PackSh( result.specRadiance, result.specHitDist, result.specDirection, outSpecSh, USE_SANITIZATION );
    #else
        outDiff = RELAX_FrontEnd_PackRadianceAndHitDist( result.diffRadiance, result.diffHitDist, USE_SANITIZATION );
        outSpec = RELAX_FrontEnd_PackRadianceAndHitDist( result.specRadiance, result.specHitDist, USE_SANITIZATION );
    #endif
    }
    else
    {
    #if( NRD_MODE == OCCLUSION )
        outDiff = result.diffHitDist;
        outSpec = result.specHitDist;
    #elif( NRD_MODE == SH )
        outDiff = REBLUR_FrontEnd_PackSh( result.diffRadiance, result.diffHitDist, result.diffDirection, outDiffSh, USE_SANITIZATION );
        outSpec = REBLUR_FrontEnd_PackSh( result.specRadiance, result.specHitDist, result.specDirection, outSpecSh, USE_SANITIZATION );
    #elif( NRD_MODE == DIRECTIONAL_OCCLUSION )
        outDiff = REBLUR_FrontEnd_PackDirectionalOcclusion( result.diffDirection, result.diffHitDist, USE_SANITIZATION );
    #else
        outDiff = REBLUR_FrontEnd_PackRadianceAndNormHitDist( result.diffRadiance, result.diffHitDist, USE_SANITIZATION );
        outSpec = REBLUR_FrontEnd_PackRadianceAndNormHitDist( result.specRadiance, result.specHitDist, USE_SANITIZATION );
    #endif
    }

    WriteResult( pixelPos, outDiff, outSpec, outDiffSh, outSpecSh );

    //================================================================================================================================================================================
    // Sun shadow
    //================================================================================================================================================================================

    float2 rnd = Rng::Hash::GetFloat2( );
    if( USE_BLUE_NOISE_FOR_SHADOWS )
        rnd = GetBlueNoise( pixelPos, gIn_ScramblingRanking4, 4, gFrameIndex );

    rnd = ImportanceSampling::Cosine::GetRay( rnd ).xy;
    rnd *= gTanSunAngularRadius;

    float3 sunDirection = normalize( gSunBasisX.xyz * rnd.x + gSunBasisY.xyz * rnd.y + gSunDirection.xyz );
    float3 Xoffset = GetXoffset( Xshadow, sunDirection, PT_SHADOW_RAY_OFFSET );
    float2 mipAndCone = GetConeAngleFromAngularRadius( geometryProps0.mip, gTanSunAngularRadius );

    float shadowTranslucency = ( Color::Luminance( Ldirect ) != 0.0 && !gDisableShadowsAndEnableImportanceSampling ) ? 1.0 : 0.0;
    float shadowHitDist = 0.0;

    while( shadowTranslucency > 0.01 )
    {
        GeometryProps geometryPropsShadow = CastRay( Xoffset, sunDirection, 0.0, INF, mipAndCone, gWorldTlas, GEOMETRY_ALL, 0 );

        // Update hit dist
        shadowHitDist += geometryPropsShadow.hitT;

        // Terminate on miss ( before updating translucency! )
        if( geometryPropsShadow.IsMiss( ) )
            break;

        // ( Biased ) Cheap approximation of shadows through glass
        float NoV = abs( dot( geometryPropsShadow.N, sunDirection ) );
        shadowTranslucency *= lerp( geometryPropsShadow.Has( FLAG_TRANSPARENT ) ? 0.9 : 0.0, 0.0, Math::Pow01( 1.0 - NoV, 2.5 ) );

        // Go to the next hit
        Xoffset += sunDirection * ( geometryPropsShadow.hitT + 0.001 );
    }

    float penumbra = SIGMA_FrontEnd_PackPenumbra( shadowHitDist, gTanSunAngularRadius );
    float4 translucency = SIGMA_FrontEnd_PackTranslucency( shadowHitDist, shadowTranslucency );

    gOut_ShadowData[ pixelPos ] = penumbra;
    gOut_Shadow_Translucency[ pixelPos ] = translucency;
}

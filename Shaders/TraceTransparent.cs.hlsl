// © 2022 NVIDIA Corporation

#include "Shared.hlsli"
#include "RaytracingShared.hlsli"

// Inputs
NRI_RESOURCE( Texture2D<float3>, gIn_ComposedDiff, t, 0, SET_OTHER );
NRI_RESOURCE( Texture2D<float4>, gIn_ComposedSpec_ViewZ, t, 1, SET_OTHER );

// Outputs
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float3>, gOut_Composed, u, 0, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gInOut_Mv, u, 1, SET_OTHER );
NRI_FORMAT("unknown") NRI_RESOURCE( RWTexture2D<float4>, gOut_Normal_Roughness, u, 2, SET_OTHER );

//========================================================================================
// TRACE TRANSPARENT
//========================================================================================

struct TraceTransparentDesc
{
    // Geometry properties
    GeometryProps geometryProps;

    // Pixel position
    uint2 pixelPos;

    // Is reflection or refraction in first segment?
    bool isReflection;
};

// TODO: think about adding a specialized delta-event denoiser in NRD:
//  Inputs:
//      - Lsum ( delta events gathered across the path )
//      - reflections or refractions prevail?
//  Principle:
//      - add missing component (reflection or refraction) from neighboring pixels
float3 TraceTransparent( TraceTransparentDesc desc )
{
    float eta = BRDF::IOR::Air / BRDF::IOR::Glass;

    GeometryProps geometryProps = desc.geometryProps;
    float pathThroughput = 1.0;
    bool isReflection = desc.isReflection;
    float bayer = Sequence::Bayer4x4( desc.pixelPos, gFrameIndex );

    [loop]
    for( uint bounce = 1; bounce <= PT_DELTA_BOUNCES_NUM; bounce++ )
    {
        // Reflection or refraction?
        float NoV = abs( dot( geometryProps.N, geometryProps.V ) );
        float F = BRDF::FresnelTerm_Dielectric( eta, NoV );

        if( bounce == 1 )
            pathThroughput *= isReflection ? F : 1.0 - F;
        else
        {
            float rnd = frac( bayer + Sequence::Halton( bounce, 3 ) ); // "Halton( bounce, 2 )" works worse than others

            // Bonus dithering to break banding // TODO: needed for "transparent machines"
            /*
            float ditherMask = Sequence::Bayer4x4( desc.pixelPos, bounce ); // or
            //float ditherMask = Rng::Hash::GetFloat( ); // or
            rnd = saturate( rnd + ( ditherMask - 0.5 ) * 0.04 );
            */

            [flatten]
            if( gRR || gSR ) // TNN prefers white noise
                rnd = Rng::Hash::GetFloat( );
            isReflection = rnd < F;
        }

        // Trace
        uint flags = bounce == PT_DELTA_BOUNCES_NUM ? FLAG_NON_TRANSPARENT : GEOMETRY_ALL;

        float3 Xoffset, ray;
        eta = GetDeltaEventRay( geometryProps, isReflection, eta, Xoffset, ray );

        geometryProps = CastRay( Xoffset, ray, 0.0, INF, GetConeAngleFromRoughness( geometryProps.mip, 0.0 ), gWorldTlas, flags, 0 );

        // TODO: ideally each "medium" should have "eta" and "extinction" parameters in "TraceTransparentDesc" and "TraceOpaqueDesc"
        bool isAir = eta < 1.0;
        float extinction = isAir ? 0.0 : 1.0; // TODO: tint color?
        if( !geometryProps.IsMiss() ) // TODO: fix for non-convex geometry
            pathThroughput *= exp( -extinction * geometryProps.hitT * gUnitToMetersMultiplier );

        // Is opaque hit found?
        if( !geometryProps.Has( FLAG_TRANSPARENT ) ) // TODO: stop if pathThroughput is low
            break;
    }

    // This is always non-transparent
    MaterialProps materialProps = GetMaterialProps( geometryProps );

    float4 Lcached = float4( materialProps.Lemi, 0.0 );
    if( !geometryProps.IsMiss( ) )
    {
        // L1 cache - reproject previous frame, carefully treating specular
        float3 prevLdiff, prevLspec;
        float reprojectionWeight = ReprojectIrradiance( false, !isReflection, gIn_ComposedDiff, gIn_ComposedSpec_ViewZ, geometryProps, desc.pixelPos, prevLdiff, prevLspec );
        Lcached = float4( prevLdiff + prevLspec, reprojectionWeight );

        // L2 cache - SHARC
        HashGridParameters hashGridParameters;
        hashGridParameters.cameraPosition = gCameraGlobalPos.xyz;
        hashGridParameters.sceneScale = SHARC_SCENE_SCALE;
        hashGridParameters.logarithmBase = SHARC_GRID_LOGARITHM_BASE;
        hashGridParameters.levelBias = SHARC_GRID_LEVEL_BIAS;

        float3 Xglobal = GetGlobalPos( geometryProps.X );
        uint level = HashGridGetLevel( Xglobal, hashGridParameters );
        float voxelSize = HashGridGetVoxelSize( level, hashGridParameters );

        float2 rndScaled = ImportanceSampling::Cosine::GetRay( Rng::Hash::GetFloat2( ) ).xy;
        rndScaled *= voxelSize;
        rndScaled *= 1.5;
        rndScaled *= USE_SHARC_DITHERING * float( USE_SHARC_DEBUG == 0 );

        float3x3 mBasis = Geometry::GetBasis( geometryProps.N );
        float3 jitter = mBasis[ 0 ] * rndScaled.x + mBasis[ 1 ] * rndScaled.y;

        SharcHitData sharcHitData;
        sharcHitData.materialDemodulation = GetMaterialFactor( geometryProps, materialProps );
        sharcHitData.normalWorld = geometryProps.N;
        sharcHitData.emissive = materialProps.Lemi;

        HashGridData hashGridData;
        hashGridData.capacity = SHARC_CAPACITY;
        hashGridData.hashEntriesBuffer = gInOut_SharcHashEntriesBuffer;

        SharcParameters sharcParams;
        sharcParams.hashGridParameters = hashGridParameters;
        sharcParams.hashGridData = hashGridData;
        sharcParams.radianceScale = SHARC_RADIANCE_SCALE;
        sharcParams.accumulationBuffer = gInOut_SharcAccumulated;
        sharcParams.resolvedBuffer = gInOut_SharcResolved;

        bool isSharcAllowed = Rng::Hash::GetFloat( ) > Lcached.w; // is needed?
        isSharcAllowed &= gSHARC && NRD_MODE < OCCLUSION; // trivial

        if( isSharcAllowed )
        {
            float3 sharcRadiance = 0;

            // Try jittered position
            sharcHitData.positionWorld = Xglobal + jitter;
            bool isFound = SharcGetCachedRadiance( sharcParams, sharcHitData, sharcRadiance, false );

            if( !isFound )
            {
                // Slipped out of the surface or mismatched normals, try non-jittered position
                sharcHitData.positionWorld = Xglobal;
                isFound = SharcGetCachedRadiance( sharcParams, sharcHitData, sharcRadiance, false );
            }

            if( isFound )
                Lcached = float4( sharcRadiance, 1.0 );
        }

        // Cache miss - compute lighting, if not found in caches
        if( Rng::Hash::GetFloat( ) > Lcached.w )
        {
            float3 L = GetLighting( geometryProps, materialProps, LIGHTING | SHADOW ) + materialProps.Lemi;
            Lcached.xyz = max( Lcached.xyz, L );
        }
    }

    // Output
    return Lcached.xyz * pathThroughput;
}

//========================================================================================
// MAIN
//========================================================================================

[numthreads( 16, 16, 1 )]
void main( int2 pixelPos : SV_DispatchThreadID )
{
    float2 pixelUv = float2( pixelPos + 0.5 ) * gInvRectSize;
    float2 sampleUv = pixelUv + gJitter;

    // Do not generate NANs for unused threads
    if( pixelUv.x > 1.0 || pixelUv.y > 1.0 )
        return;

    // Initialize RNG
    Rng::Hash::Initialize( pixelPos, gFrameIndex );

    // Primary ray for transparent geometry only
    float3 cameraRayOrigin = ( float3 )0;
    float3 cameraRayDirection = ( float3 )0;
    GetCameraRay( cameraRayOrigin, cameraRayDirection, sampleUv );

    float viewZAndTaaMask = gInOut_Mv[ pixelPos ].w;
    float viewZ = Math::Sign( gNearZ ) * abs( viewZAndTaaMask ) / FP16_VIEWZ_SCALE; // viewZ before PSR
    float3 Xv = Geometry::ReconstructViewPosition( sampleUv, gCameraFrustum, viewZ, gOrthoMode );
    float tmin0 = gOrthoMode == 0 ? length( Xv ) : abs( Xv.z );

    GeometryProps geometryPropsT = CastRay( cameraRayOrigin, cameraRayDirection, 0.0, tmin0, GetConeAngleFromRoughness( 0.0, 0.0 ), gWorldTlas, FLAG_TRANSPARENT, 0 );

    float3 Lsum = 0;
    if( !geometryPropsT.IsMiss( ) && geometryPropsT.hitT < tmin0 && gOnScreen == SHOW_FINAL )
    {
        // Append "glass" mask to "hair" mask
        viewZAndTaaMask = -abs( viewZAndTaaMask );

        // Patch motion vectors replacing MV for the background with MV for the closest glass layer.
        // IMPORTANT: surface-based motion can be used only if the object is curved.
        // TODO: let's use the simplest heuristic for now, but better switch to some "smart" interpolation between
        // MVs for the primary opaque surface hit and the primary glass surface hit.
        float3 mvT = GetMotion( geometryPropsT.X, geometryPropsT.Xprev );
        gInOut_Mv[ pixelPos ] = float4( mvT, viewZAndTaaMask );

        // Patch guides for RR
        [branch]
        if( gRR )
            gOut_Normal_Roughness[ pixelPos ] = NRD_FrontEnd_PackNormalAndRoughness( geometryPropsT.N, 0.0, 0 );

        // Trace transparent stuff
        TraceTransparentDesc desc = ( TraceTransparentDesc )0;
        desc.geometryProps = geometryPropsT;
        desc.pixelPos = pixelPos;

        // IMPORTANT: use 1 reflection path and 1 refraction path at the primary glass hit to significantly reduce noise
        desc.isReflection = true;
        float3 reflection = TraceTransparent( desc );
        Lsum = reflection;

        desc.isReflection = false;
        float3 refraction = TraceTransparent( desc );
        Lsum += refraction;
    }
    else
    {
        // Composed without glass
        float3 diff = gIn_ComposedDiff[ pixelPos ];
        float3 spec = gIn_ComposedSpec_ViewZ[ pixelPos ].xyz;

        Lsum = diff + spec;
    }

    // Output
    gOut_Composed[ pixelPos ] = Lsum * gExposure; // apply exposure
}
